/* Copyright (c) 2006, Sun Microsystems, Inc.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     * Redistributions of source code must retain the above copyright notice,
 *       this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the Sun Microsystems, Inc. nor the names of its
 *       contributors may be used to endorse or promote products derived from
 *       this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
 * THE POSSIBILITY OF SUCH DAMAGE.
 */


options {
  MULTI=true;
  KEEP_LINE_COLUMN = false;
  STATIC = false;
}

PARSER_BEGIN(Eg2)

/** An Arithmetic Grammar. */
//package Parser;
import symbols.*;

public class Eg2
{
	Env curEnv = null;       // current symbol table
	/** Main entry point. */
	public static void main(String args[])
	{
	  	System.out.println("Reading from standard input...");
	    Eg2 t = new Eg2(System.in);
	    try
	    {
	      ASTStart n = t.Start();
	      n.dump("");
	      System.out.println("Thank you.");
	    } catch (Exception e)
	    {
	      System.out.println("Oops.");
	      System.out.println(e.getMessage());
	      e.printStackTrace();
	    }
	}
}

PARSER_END(Eg2)


SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n") >
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN : /* KEYWORD  */
{
	<IF: "if" >
	| <ELSE: "else" >
	| <FOR: "for" >
	| <TO: "to" >
	| <WHILE: "while" >
	| <FOREACH: "foreach" >
	| <BREAK: "break" >
	| <CONTINUE: "continue" >
	| <STRUCT: "struct" >
	| <RETURN: "return" >
}


TOKEN : /* KEYWORD  */
{
  	
	<VARIABLE_TYPE:  <BOOL > | <INT > | <CHAR > | <REAL >>
	| <CONST: "const" >
	| <#BOOL: "bool" >
	| <#INT: "int" >
	| <#CHAR: "char" >
	| <#REAL: "real" >
	| <VOID: "void" >
}


TOKEN : /* LITERAL  */
{
  	<INTEGER_LITERAL:
		<DECIMAL_NUMERAL >
		| <HEX_NUMERAL >
		| <OCTAL_NUMERAL >
	>
	| <REAL_LITERAL:
		(<DECIMAL_NUMERAL > "." <DECIMAL_NUMERAL >)
		| ("." <DECIMAL_NUMERAL >)
		| (<DECIMAL_NUMERAL > ".")
	>
	| <BOOLEAN_LITERAL: "true" | "false" >
	| <NULL_LITERAL: "null" >
	| <CHARACTER_LITERAL: ("\'" ["a"-"z", "A"-"Z", "1"-"9"] "\'") >
	| <#NON_ZERO_DIGIT: ["1"-"9"] >
	| <#DIGIT: "0" | <NON_ZERO_DIGIT > >
	| <#DIGITS: (<DIGIT >)* >
	| <#DECIMAL_NUMERAL:
		"0"
		|<NON_ZERO_DIGIT ><DIGITS >
	>
	| <#HEX_DIGIT: ["0"-"9", "a"-"f", "A"-"F"] >	
	| <#HEX_NUMERAL: ("0" ["x", "X"] (<HEX_DIGIT >)+) >
	| <#OCTAL_DIGIT: ["0"-"7"] >
	| <#OCTAL_NUMERAL: ("0" (<OCTAL_DIGIT >)+) >	}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER: ["_","a"-"z","A"-"Z"] >
}

TOKEN : /* ASSIGNMENT OPERATORS */
{
  	<ASSIGNMENT_EQUAL_OPERATOR: "=" >
	| <ASSIGNMENT_OPERATOR:
		"*="
		| "/="
		| "+="
		| "-="
		| "<<="
		| ">>="
		| "&="
		| "^="
		| "|="
	>}



/** Main production. */
ASTStart Start() : {}
{
	{		
		curEnv = new Env(null);
	}
	
	( StructDeclaration() | LOOKAHEAD(3)FunctionDeclarator() | LOOKAHEAD(3)UniversalDeclarator())+
	
	{
		return jjtThis;
	}
}

/** ************Declarations*************** .*/

/** array initializer. */
void ArrayInitializer() : {}
{
	Expression() ("," Expression())*
}

void UniversalDeclarator() : 
{
	Token t = null;
	String id;
}
{
	(<CONST >)? t = <VARIABLE_TYPE > id = MyID()
	{
		jjtThis.setName(t.image);
		if (curEnv.contains(id))
			throw new ParseException("Redifine: "+ id);
		String s = t.image;
		if (s.compareTo("int") == 0)
		   curEnv.put(id, Type.Int);
		else if (s.compareTo("bool") == 0)
			curEnv.put(id, Type.Bool);
		else if (s.compareTo("real") == 0)
			curEnv.put(id, Type.Real);
		else if (s.compareTo("char") == 0)
			curEnv.put(id, Type.Char);
		
	}	
	(
		<ASSIGNMENT_EQUAL_OPERATOR > Expression()
	|
		"[" (<INTEGER_LITERAL >)? "]" (<ASSIGNMENT_EQUAL_OPERATOR > "{" ArrayInitializer() "}")?
	)?
	(
		"," id = MyID()
		{
			if (curEnv.contains(id))
				throw new ParseException("Redifine: "+ id);
			curEnv.put(id, new Type(t.image, false));		}
		(
		<ASSIGNMENT_EQUAL_OPERATOR > Expression()
		|
			"[" (<INTEGER_LITERAL >)? "]" (<ASSIGNMENT_EQUAL_OPERATOR > "{" ArrayInitializer() "}")?
		)?
	)*
	";"
}

/** struct declaration. */
void StructDeclaration() : 
{
	Token t = null;
	String id;
}
{
	t = "struct" id = MyID()
	{
		if (curEnv.contains(id))
			throw new ParseException("Redifine: "+ id);
		curEnv.put(id, new Type(t.image, true));	}
	StructBody()
}

/** struct body. */
void StructBody() :
{
	Env savedEnv = curEnv;
	curEnv = new Env(curEnv);
}
{
	"{"
	StructDeclarations()	
	"}"
	
	{
		curEnv = savedEnv;	}
}

/** struct declarations. */
void StructDeclarations() : {}
{
	  (StructParameter())*
}

/** struct parameter. */
void StructParameter() : {}
{
	UniversalDeclarator()
}

/** function declarator. */
void FunctionDeclarator() :
{
	Token t;
	String id;
}
{
	(t = <VOID > | t = <VARIABLE_TYPE >)
	id = MyID()
	{
		jjtThis.setType(t.image);
		jjtThis.setName(id);
		jjtThis.genInter();
		if (curEnv.contains(id))
			throw new ParseException("Redifine: "+ id);
		curEnv.put(id, new Type(t.image, true));
		Env savedEnv = curEnv;
		curEnv = new Env(curEnv);
	}
	"(" (FunctionParameterList())? ")" 
	{
		jjtThis.writeReturn();
	}
	FunctionBody()
	{
		curEnv = savedEnv;	}
}

/** function parameter list. */
void FunctionParameterList() : {}
{
	FunctionParameter() ("," FunctionParameter())*
}

/** function parameter. */
void FunctionParameter() :
{
	Token t;
	String id;
}
{
	(<CONST >)? t = <VARIABLE_TYPE > id = MyID()
	{
		jjtThis.setType(t.image);
		jjtThis.setName(id);
		jjtThis.genInter();
		
		if (curEnv.contains(id))
			throw new ParseException("Redifine: "+ id);
		curEnv.put(id, new Type(t.image, false));
	}
	(	  <ASSIGNMENT_EQUAL_OPERATOR > Expression()
	  | "[" (<INTEGER_LITERAL >)? "]" (<ASSIGNMENT_EQUAL_OPERATOR > "{" ArrayInitializer() "}")?
	)?
}

/** function body. */
void FunctionBody() : {}
{
	"{" BlockStatements() "}"
}



/** ************block*************** .*/

/** A block. */
void Block() : {}
{
	"{" BlockStatements() "}"
	| Statement()
}

/** block statements. */
void BlockStatements() : {}
{
	(BlockStatement())*
}

/** block statement. */
void BlockStatement() : {}
{
	UniversalDeclarator()
	| Statement()
}

/** A statement. */
void Statement() : {}
{
	SimpleStatement()
	| IfStatement()
	| WhileStatement()
	| ForStatement()
	| ForeachStatement()
}


/** statement without trailing substatement(simple statement). */
void SimpleStatement() : {}
{
	EmptyStatement()
	| LOOKAHEAD(10) (PostIncDecExpression() ";")
	| LOOKAHEAD(10) (PreIncDecExpression() ";")
	| LOOKAHEAD(10) (AssignmentExpression() ";")	
	| BreakStatement()
	| ContinueStatement()
	| ReturnStatement()
}

/** empty statement. */
void EmptyStatement() : {}
{
	";"
}



/** if statement. */
void IfStatement() :
{
	int paraR;
}
{
	<IF > "(" paraR = Expression() ")"
	{
		jjtThis.genInter(0, paraR);	}
	Block()
	{
	 	jjtThis.genTailLabel();	}
	(LOOKAHEAD(2) <ELSE > <IF > "(" Expression() ")" Block())* (<ELSE > Block())?
	
}

/** A WhileStatement. */
void WhileStatement() : 
{
	int paraR;
}
{	
	<WHILE >
	{
		jjtThis.writeHeadLabel();
	}
	"(" paraR = Expression() ")" 
	{
		jjtThis.genInter(0, paraR);		
	}
	Block()
	{
		jjtThis.writeTailLabel();
	}
}

/** for statement. */
void ForStatement() : 
{
	int paraL, paraR;
	String id;
}
{
	
	<FOR >
	{
		jjtThis.writeHeadLabel();
	}
	"(" (<VARIABLE_TYPE >)? id = MyID() <ASSIGNMENT_EQUAL_OPERATOR > paraL = Expression() <TO > paraR = Expression() ")" 
	{
		jjtThis.genInter(id, paraL, paraR);
	}
	Block()
	{
		jjtThis.writeTailLabel();
	}
}

/** foreach statement. */
void ForeachStatement() : {}
{
	<FOREACH > "(" <VARIABLE_TYPE > MyID() ":" MyID() ")" Block()
}

/** break statement. */
void BreakStatement() : {}
{
	<BREAK > ";"
}

/** continue statement. */
void ContinueStatement() : {}
{
	<CONTINUE > ";"
}

/** return statement. */
void ReturnStatement() : 
{
	int paraL;
}
{
	<RETURN >
	(
		paraL = Expression()
		{
			jjtThis.genInter(paraL, 0);		}
	)? ";"
}

/** comment statement. */
void CommentStatement() : {}
{
	";"
}


/** ************Expressions*************** .*/

/** An Expression. */
int Expression() :{}
{
	LOOKAHEAD(5)
	AssignmentExpression()
	{
		return jjtThis.getPara();
	}
	| ConditionalExpression()
	{
		return jjtThis.getPara();	}
}

/** assignment expression. */
int AssignmentExpression() :
{
	String lhs;
	Token op = null;
	int paraR;
}
{
	lhs = LeftSide() (op = <ASSIGNMENT_OPERATOR > | op = <ASSIGNMENT_EQUAL_OPERATOR >) paraR = Expression()
	{
		Type type = curEnv.get(lhs);
		if (type == null)
			throw new ParseException("Undifine: "+ lhs);
		if (type.isFunc)
			throw new ParseException("Invalid parameter: "+ lhs);
		if ((type == Type.Bool) && (jjtThis.typeRight != Type.Bool))
			throw new ParseException("Error type of op: " + op.image + " / "+ type.name + " " + jjtThis.typeRight.name);
		if ((type != Type.Bool) && (jjtThis.typeRight == Type.Bool))
			throw new ParseException("Error type of op: " + op.image + " / "+ type.name + " " + jjtThis.typeRight.name);
		if ((type == Type.Char) && (jjtThis.typeRight == Type.Real))
			throw new ParseException("Error type of op: " + op.image + " / "+ type.name + " " + jjtThis.typeRight.name);
		if ((type == Type.Int) && (jjtThis.typeRight == Type.Real))
			throw new ParseException("Error type of op: " + op.image + " / "+ type.name + " " + jjtThis.typeRight.name);
		jjtThis.typeRight = type;						
		jjtThis.setLeftSide(lhs);
		jjtThis.setOp(op.image);
		return jjtThis.genInter(0, paraR);
	}
}

/** conditional expression. */
int ConditionalExpression() : {}
{
	ConditionalOrExpression() ("?" Expression() ":" Expression())?
	{
		return jjtThis.getPara();	}
}

/** conditional or expression. */
int ConditionalOrExpression() :
{
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = ConditionalAndExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		"||" paraR = ConditionalAndExpression()
		
		{
			if ((typeLeft != Type.Bool) || (jjtThis.typeRight != Type.Bool))
				throw new ParseException("Error type of op: || / " + typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.Bool;			paraL = jjtThis.genInter(paraL, paraR);		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** conditional and expression. */
int ConditionalAndExpression() :
{
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = InclusiveOrExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		"&&" paraR = InclusiveOrExpression()
		{
			if ((typeLeft != Type.Bool) || (jjtThis.typeRight != Type.Bool))
				throw new ParseException("Error type of op: && / " + typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.Bool;
			paraL =  jjtThis.genInter(paraL, paraR);		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** inclusive or expression. */
int InclusiveOrExpression() : 
{
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = ExclusiveOrExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		"|" paraR = ExclusiveOrExpression()
		{
			if (typeLeft != Type.Int && typeLeft != Type.Char)
				throw new ParseException("Error type of op: | / " + typeLeft.name + " " + jjtThis.typeRight.name);
			if (jjtThis.typeRight != Type.Int && jjtThis.typeRight != Type.Char)
				throw new ParseException("Error type of op: | / " + typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.max(typeLeft, jjtThis.typeRight);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** exclusive or expression. */
int ExclusiveOrExpression() :
{
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = AndExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		"^" paraR = AndExpression()
		{
			if (typeLeft != Type.Int && typeLeft != Type.Char)
				throw new ParseException("Error type of op: ^ / " + typeLeft.name + " " + jjtThis.typeRight.name);
			if (jjtThis.typeRight != Type.Int && jjtThis.typeRight != Type.Char)
				throw new ParseException("Error type of op: ^ / " + typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.max(typeLeft, jjtThis.typeRight);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** and expression. */
int AndExpression() :
{
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = EqualityExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		"&" paraR = EqualityExpression()
		{
			if (typeLeft != Type.Int && typeLeft != Type.Char)
				throw new ParseException("Error type of op: & / " + typeLeft.name + " " + jjtThis.typeRight.name);
			if (jjtThis.typeRight != Type.Int && jjtThis.typeRight != Type.Char)
				throw new ParseException("Error type of op: & / " + typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.max(typeLeft, jjtThis.typeRight);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}


/** equality expression. */
int EqualityExpression() : 
{
	int paraL, paraR;
	Token op = null;
	Type typeLeft;
}
{
	paraL = RelationalExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		(op = "==" | op = "!=") paraR = RelationalExpression()
		{
			if ((typeLeft == Type.Bool) && Type.numeric(jjtThis.typeRight))
				throw new ParseException("Error type of op: " + op.image + " / "+ typeLeft.name + " " + jjtThis.typeRight.name);
			if ((jjtThis.typeRight == Type.Bool) && Type.numeric(typeLeft))
				throw new ParseException("Error type of op: " + op.image + " / "+ typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.Bool;
			jjtThis.setOp(op.image);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** relational expression. */
int RelationalExpression() :
{
	Token op = null;
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = ShiftExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		(op = "<"| op = ">" | op = "<=" | op = ">=") paraR = ShiftExpression()
		{
			if ((!Type.numeric(typeLeft)) || (!Type.numeric(jjtThis.typeRight)))
				throw new ParseException("Error type of op: " + op.image + " / "+ typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.Bool;
			jjtThis.setOp(op.image);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** shift expression. */
int ShiftExpression() :
{
	Token op = null;
	int paraL, paraR;
	Type typeLeft;
}
{
	paraL = AdditiveExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		(op = "<<" | op = ">>") paraR = AdditiveExpression()
		{
			if (typeLeft != Type.Int && typeLeft != Type.Char)
				throw new ParseException("Error type of op: " + op.image + " / "+ typeLeft.name + " " + jjtThis.typeRight.name);
			if (jjtThis.typeRight != Type.Int && jjtThis.typeRight != Type.Char)
				throw new ParseException("Error type of op: " + op.image + " / "+ typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = Type.Int;
			jjtThis.setOp(op.image);
			paraL =  jjtThis.genInter(paraL, paraR);
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** additive expression. */
int AdditiveExpression() :
{
	Token op = null;
	int paraL, paraR;
	Type typeLeft, temp;
}
{
	paraL = MultiplicativeExpression()
	{
		typeLeft = jjtThis.typeRight;
	}
	(
		(op = "+" | op = "-") paraR = MultiplicativeExpression()
		{
			jjtThis.setOp(op.image);
			paraL =  jjtThis.genInter(paraL, paraR);
			temp = Type.max(typeLeft, jjtThis.typeRight);
			if (temp == null)
				throw new ParseException("Unmatched type: "+ typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = temp;
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
}

/** multiplicative expression. */
int MultiplicativeExpression() :
{
	Token op = null;
	int paraL, paraR;
	Type typeLeft, temp;
}
{
	paraL = UnaryExpression()
	{
		typeLeft = jjtThis.typeRight;	}
	(
		(op = "*" | op = "/" | op = "%") paraR = UnaryExpression()
		{
			jjtThis.setOp(op.image);
			paraL = jjtThis.genInter(paraL, paraR);
			temp = Type.max(typeLeft, jjtThis.typeRight);
			if (temp == null)
				throw new ParseException("Unmatched type: "+ typeLeft.name + " " + jjtThis.typeRight.name);
			typeLeft = temp;
		}
	)*
	{
		jjtThis.typeRight = typeLeft;
		return paraL;	}
	
}


/** unary expression. */
int UnaryExpression() : 
{
	Token op = null;
	int paraR;
}
{
	SimpleUnaryExpression()
	{
		return jjtThis.getPara();	}
	| op = "+" paraR = UnaryExpression()
	{
		if (!Type.numeric(jjtThis.typeRight))
			throw new ParseException("Error type of op: +  / " +  jjtThis.typeRight.name);
		jjtThis.setOp(op.image);
		return jjtThis.genInter(0, paraR);	}
	| op = "-" paraR = UnaryExpression()
	{
		if (!Type.numeric(jjtThis.typeRight))
			throw new ParseException("Error type of op: -  / " + jjtThis.typeRight.name);
		jjtThis.setOp(op.image);
		return jjtThis.genInter(0, paraR);	}
}

/** PreIncDecExpression. */
void PreIncDecExpression() : {}
{
	("--" | "++") LeftSide()
}


/** PostIncDecExpression. */
void PostIncDecExpression() : {}
{
	LeftSide() ("--" | "++")
}


/** unary expression not plus minus(simpleUnaryExpression). */
int SimpleUnaryExpression() : 
{
	Token op = null;
	int paraR;
}
{
	Primary()
	{
		return jjtThis.getPara();
	}
	| op = "~" paraR = UnaryExpression()
	{
		if (jjtThis.typeRight != Type.Int && jjtThis.typeRight != Type.Char)
			throw new ParseException("Error type of op: ~ / " + jjtThis.typeRight.name);
		jjtThis.setOp(op.image);
		return jjtThis.genInter(0, paraR);
	}
	| op = "!" paraR = UnaryExpression()
	{
		if (Type.numeric(jjtThis.typeRight))
			throw new ParseException("Error type of op: ! / " + jjtThis.typeRight.name);
		jjtThis.setOp(op.image);
		return jjtThis.genInter(0, paraR);
	}
}


/** primary. */
int Primary() :
{
	Token t1;
	String lhs;
}
{
  	t1 = <INTEGER_LITERAL >
	{
		jjtThis.setName(t1.image);
		jjtThis.typeRight = Type.Int;
		return jjtThis.genInter();
	}
	| t1 = <REAL_LITERAL >
	{
		jjtThis.setName(t1.image);		
		jjtThis.typeRight = Type.Real;
		return jjtThis.genInter();
	}
	| t1 = <BOOLEAN_LITERAL >
	{
		jjtThis.setName(t1.image);		
		jjtThis.typeRight = Type.Bool;
		return jjtThis.genInter();
	} 
	| t1 = <CHARACTER_LITERAL >
	{
		jjtThis.setName(t1.image);		
		jjtThis.typeRight = Type.Char;
		return jjtThis.genInter();
	}	
	| lhs = LeftSide() (FunctionInvoke())?
	{
		Type type = curEnv.get(lhs); 
		if (type == null)
			throw new ParseException("Undifine: "+ lhs);
		if (type.isFunc)
			throw new ParseException("Invalid parameter: "+ lhs);
		jjtThis.typeRight = type;
		jjtThis.setName(lhs);
		return jjtThis.genInter();		
	}
	| "(" Primary2()
}

/** FunctionInvoke. */
void FunctionInvoke() : {}
{
	"(" (Expression() ("," Expression())*)?	")"
}


/** primary2. */
void Primary2() : {}
{
	Expression() ")"
	| <VARIABLE_TYPE > ")" SimpleUnaryExpression() //cast expression}

/** left side. */
String LeftSide() :
{
	Token t1;
	Token t2;
}
{
	t1 = <IDENTIFIER>
	{
		jjtThis.setName(t1.image);
		return t1.image;
	}
	(
		"." t2 = <IDENTIFIER >
		{
			jjtThis.setName(t1.image + "." + t2.image);
		}
		|
		"[" Expression() "]"
	)?
}

/** An Identifier. */
String MyID() :
{
	Token t;
}
{
	t = <IDENTIFIER>
	{
		jjtThis.setName(t.image);
		return t.image;
	}
}

/**bugs remain:*/
/**
	s[i];
	s[i++];
*/